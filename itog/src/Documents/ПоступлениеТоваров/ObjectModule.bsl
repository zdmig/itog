
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
СуммаДокумента = Товары.Итог("Сумма");	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество,
		|	СРЕДНЕЕ(ПоступлениеТоваровТовары.Цена) КАК Цена,
		|	СУММА(ПоступлениеТоваровТовары.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровТовары.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// регистр ТоварыНаСкладах Приход
	Движения.ТоварыНаСкладах.Записывать = Истина;
    // регистр ЦеныНоменклатуры
	Движения.ЦеныНоменклатуры.Записывать = Истина; 
	// регистр Хозрасчетный 
	Движения.Хозрасчетный.Записывать = Истина; 
	
	 
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		Движение = Движения.ТоварыНаСкладах.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
		Движение.Сумма = РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыборкаДетальныеЗаписи.Сумма,Валюта,ОсновнаяВалюта(),Дата);
		Движение.Партия = Ссылка;

		
		Движение = Движения.ЦеныНоменклатуры.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Характеристика = Ссылка;
		Движение.ВидЦены = Перечисления.ВидыЦен.Закупочная;
		Движение.Цена = РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыборкаДетальныеЗаписи.Цена,Валюта,ОсновнаяВалюта(),Дата);

		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.ТоварыНаСкладах;
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
		Движение.Период = Дата; 	
		Движение.Сумма = РаботаСКурсамиВалют.ПересчитатьВВалюту(ВыборкаДетальныеЗаписи.Сумма,Валюта,ОсновнаяВалюта(),Дата);
		Движение.ВалютнаяСуммаКт = ВыборкаДетальныеЗаписи.Сумма;
		Движение.КоличествоДт = ВыборкаДетальныеЗаписи.Количество; 
		Движение.ВалютаКт = Валюта;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Характеристики] = Ссылка;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Характеристики] = Ссылка;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Поставщик;

	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		// Заполнение шапки
		Валюта = Справочники.Валюты.НайтиПоРеквизиту("НаименованиеПолное", "Российский рубль");
		Поставщик = ДанныеЗаполнения.Поставщик;
		Склад = ДанныеЗаполнения.Склад;
		Основание = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Количество = ТекСтрокаТовары.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
			НоваяСтрока.Сумма = ТекСтрокаТовары.Сумма;
			НоваяСтрока.Цена = ТекСтрокаТовары.ЦенаЗакупки;	
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Автор = Справочники.Пользователи.ПустаяСсылка();
КонецПроцедуры


